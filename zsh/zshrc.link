##### kcrane's zsh config #####

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles
export ZSH_CACHE_DIR="$HOME/.cache/zsh"
mkdir -p $ZSH_CACHE_DIR

# your project folder that we can `c [tab]` to
export PROJECTS=~/workspace

# Move cursor between words with Bash behavior (must be called before plugins)
autoload -U select-word-style && select-word-style bash


##### Source all .zsh files #####

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]; then
  source ~/.localrc
fi

# find all of our zsh files
typeset -U config_files
config_files=($ZSH/*/*.zsh)

# First, source all path.zsh files
for path_file in ${(M)config_files:#*/path.zsh}; do
  source $path_file
done

# Next, source all *.zsh files (except path.zsh and completion.zsh)
for config_file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source $config_file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit && compinit -d $ZSH_CACHE_DIR/zcompdump-$ZSH_VERSION

# load every completion after autocomplete loads
for completion_file in ${(M)config_files:#*/completion.zsh}; do
  source $completion_file
done

unset config_files path_file config_file completion_file


##### Extra configs #####
# NB: can be moved to misc.zsh if too unwieldy here

# Allow the shell to be pretty
autoload -U colors && colors
setopt auto_cd              # Automatically cd if you type just a directory name
setopt interactivecomments  # Recognize comments on the CLI
